#Использовать gitrunner
#Использовать tempfiles
#Использовать fs

Перем Лог;

Процедура УстановитьПакет(Знач Каталог, ПутьКМанифестуСборки)

	ПутьКGitsync = ОбъединитьПути("bin", "gitsync");

	Если Не ФС.Существует(ПутьКGitsync) Тогда
		ВызватьИсключение СтрШаблон("Не найден каталог %1.
		| Необходимо выполнить задание install-gitsync", ПутьКGitsync);
	КонецЕсли;

	Лог.Информация("Каталог сборки <%1>", Каталог);

	Лог.Информация("=== Сборка пакета с плагинами ===");
	КомандаOpm = Новый Команда;
	КомандаOpm.УстановитьРабочийКаталог(Каталог);
	КомандаOpm.УстановитьКоманду("opm");
	КомандаOpm.ДобавитьПараметр("build");
	КомандаOpm.ДобавитьПараметр("--mf");
	КомандаOpm.ДобавитьПараметр(ПутьКМанифестуСборки);
	КомандаOpm.ДобавитьПараметр(Каталог);
	КомандаOpm.ДобавитьЛогВыводаКоманды("task.testing-build");

	КодВозврата = КомандаOpm.Исполнить();

	Если КодВозврата <> 0  Тогда
		ВызватьИсключение КомандаOpm.ПолучитьВывод();
	КонецЕсли;

	МассивФайлов = НайтиФайлы(Каталог, "*.ospx");

	Если МассивФайлов.Количество() = 0 Тогда
		ВызватьИсключение Новый ИнформацияОбОшибке("Ошибка создания пакета gitsync-plugins", "Не найден собранный файл пакета gitsync-plugins");
	КонецЕсли;

	ФайлПлагина = МассивФайлов[0].ПолноеИмя;

	ИсполнительGitSync = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "bin/gitsync/src/cmd/gitsync.os");

	Лог.Информация("=== Установка плагинов из файла <%1> ===", ФайлПлагина);

	КаталогПлагинов = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(КаталогПлагинов);

	Сообщить("Каталог плагинов: " + КаталогПлагинов);
	УстановитьПеременнуюСреды("GITSYNC_PLUGINS_PATH", КаталогПлагинов);

	КомандаOpm = Новый Команда;
	КомандаOpm.УстановитьРабочийКаталог(Каталог);
	КомандаOpm.УстановитьКоманду("oscript");
	КомандаOpm.ДобавитьПараметр(ИсполнительGitSync);
	КомандаOpm.ДобавитьПараметр("p i");
	КомандаOpm.ДобавитьПараметр("-f");
	КомандаOpm.ДобавитьПараметр(ФайлПлагина);
	КомандаOpm.ДобавитьЛогВыводаКоманды("task.testing-build");

	КодВозврата = КомандаOpm.Исполнить();

	Если КодВозврата <> 0  Тогда
		ВызватьИсключение СтрШаблон("Ошибка установки плагинов из <%1> по причине <%2>", ФайлПлагина, КомандаOpm.ПолучитьВывод());
	КонецЕсли;

	ВременныеФайлы.УдалитьФайл(ФайлПлагина);

КонецПроцедуры

Процедура ПолезнаяРабота()

	ПутьКМанифестуСборки = "packagedef";

	УстановитьПакет(ОбъединитьПути(ТекущийСценарий().Каталог, ".."), ПутьКМанифестуСборки);

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("task.testing-build");

ПолезнаяРабота();
