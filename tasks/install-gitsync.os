#Использовать gitrunner
#Использовать tempfiles
#Использовать fs

Перем Лог;

Процедура ПолучитьИсходники(Знач URLРепозитория, Знач Ветка, Знач Каталог)

	ГитРепозиторий = Новый ГитРепозиторий;

	ГитРепозиторий.УстановитьРабочийКаталог(Каталог);

	ГитРепозиторий.КлонироватьРепозиторий(URLРепозитория, Каталог);
	Лог.Информация("Извлекаю изменения с удаленного сервера");
	ГитРепозиторий.Извлечь();
	
	Лог.Информация("Доступные ветки");
	СписокВеток = ГитРепозиторий.ПолучитьСписокВеток(Истина);

	Для каждого СтрокаТЧ Из СписокВеток Цикл
		
		Лог.Информация("Ветка <%1>", СтрокаТЧ.Имя);

	КонецЦикла;

	ГитРепозиторий.ПерейтиВВетку(Ветка, , Истина);

КонецПроцедуры

Процедура УстановитьПакет(Знач Каталог, ПутьКМанифестуСборкиGitsync)
	
	Лог.Информация("Каталог сборки <%1>", Каталог);

	Лог.Информация("Установка gitsync-plugins локально");

	КомандаOpm = Новый Команда;
	КомандаOpm.УстановитьРабочийКаталог(Каталог);
	КомандаOpm.УстановитьКоманду("opm");
	КомандаOpm.ДобавитьПараметр("install");
	КомандаOpm.ДобавитьПараметр("-l");
	КомандаOpm.ДобавитьЛогВыводаКоманды("task.install-gitsync");

	КодВозврата = КомандаOpm.Исполнить();

	Лог.Информация("Сборка пакета gitsync");
	
	КомандаOpm = Новый Команда;
	КомандаOpm.УстановитьРабочийКаталог(Каталог);
	КомандаOpm.УстановитьКоманду("opm");
	КомандаOpm.ДобавитьПараметр("build");
	КомандаOpm.ДобавитьПараметр("--mf");
	КомандаOpm.ДобавитьПараметр(ПутьКМанифестуСборкиGitsync);
	КомандаOpm.ДобавитьПараметр(Каталог);
	КомандаOpm.ДобавитьЛогВыводаКоманды("task.install-gitsync");

	КодВозврата = КомандаOpm.Исполнить();

	Если КодВозврата <> 0  Тогда
		ВызватьИсключение КомандаOpm.ПолучитьВывод();
	КонецЕсли;

	МассивФайлов = НайтиФайлы(Каталог, "*.ospx");

	Если МассивФайлов.Количество() = 0 Тогда
		ВызватьИсключение Новый ИнформацияОбОшибке("Ошибка создания пакета gitsync", "Не найден собранный файл пакета gitsync");
	КонецЕсли;

	ФайлПакета = МассивФайлов[0].ПолноеИмя;

	Лог.Информация("Установка gitsync из <%1> локально", ФайлПакета);

	КаталогУстановки = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "bin");

	УстановитьПеременнуюСреды("OSCRIPTBIN", КаталогУстановки);

	КаталогПроекта = ОбъединитьПути(ТекущийСценарий().Каталог, "..");
	
	КомандаOpm = Новый Команда;
	КомандаOpm.УстановитьРабочийКаталог(КаталогПроекта);
	КомандаOpm.УстановитьКоманду("opm");
	КомандаOpm.ДобавитьПараметр("install");
	КомандаOpm.ДобавитьПараметр("-f");
	КомандаOpm.ДобавитьПараметр(ФайлПакета);
	КомандаOpm.ДобавитьПараметр("--dest");
	КомандаOpm.ДобавитьПараметр(КаталогУстановки);
	КомандаOpm.ДобавитьЛогВыводаКоманды("task.install-gitsync");

	КодВозврата = КомандаOpm.Исполнить();

	Если КодВозврата <> 0  Тогда
		ВызватьИсключение СтрШаблон("Ошибка установки gitsync из <%1> по причине <%2>", ФайлПакета, КомандаOpm.ПолучитьВывод());
	КонецЕсли;

КонецПроцедуры

Процедура ПолезнаяРабота()

	URLРепозитория = "https://github.com/ovcharenko-di/gitsync.git";
	КаталогСборки = ВременныеФайлы.СоздатьКаталог();
	Ветка = "pr-87";
	ПутьКМанифестуСборкиGitsync = "build_packagedef";
	
	ПолучитьИсходники(URLРепозитория, Ветка, КаталогСборки);
	УстановитьПакет(КаталогСборки, ПутьКМанифестуСборкиGitsync);

	ВременныеФайлы.УдалитьФайл(КаталогСборки);

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("task.install-gitsync");

ПолезнаяРабота();
